plugins {
    id 'fabric-loom' version '1.11.5' apply(false)
    id 'net.neoforged.moddev' version '2.0.107' apply(false)
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply(false)
    id("org.spongepowered.mixin") version "0.7.38" apply(false)
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(21)

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            def specTitleProvider = providers.provider { mod_name }
            def specVendorProvider = providers.provider { mod_author }
            def versionProvider = providers.provider { project.jar.archiveVersion.get() }
            def implTitleProvider = providers.provider { project.name }
            def timestampProvider = providers.provider { new Date().format("yyyy-MM-dd'T'HH:mm:ssZ") }
            def javaVersionProvider = providers.provider { "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})" }
            def mcVersionProvider = providers.provider { minecraft_version }

            attributes(
                'Specification-Title': specTitleProvider,
                'Specification-Vendor': specVendorProvider,
                'Specification-Version': versionProvider,
                'Implementation-Title': implTitleProvider,
                'Implementation-Version': versionProvider,
                'Implementation-Vendor': specVendorProvider,
                'Implementation-Timestamp': timestampProvider,
                'Built-On-Java': javaVersionProvider,
                'Built-On-Minecraft': mcVersionProvider,
            )
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (JEI / CraftTweaker / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = 'UTF-8'
        it.options.release = 21
    }

    processResources {
        def propsProvider = providers.provider { project.properties }
        filesMatching(['pack.mcmeta', 'fabric.mod.json', 'neoforge.mods.toml', '*.mixins.json']) {
            expand(propsProvider.get())
        }
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }
}
